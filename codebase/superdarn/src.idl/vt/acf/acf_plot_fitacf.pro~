;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;+
;NAME:
; acf_plot_fitacf
;
; PURPOSE:
; Plots the results of fitting ACFs from 1 beam sounding with the fitacf algorithm
;
; CATEGORY:
; Graphics
;
; CALLING SEQUENCE:
; first call the c routine test_fitacf, e.g.
; 	test_fitacf [-new] -hr 5 -min 3 -beam 7 myfile.rawacf > /rst/output_files/timestamp.fitacf.test
;
; next, call the IDL routine, e.g.
; 	acf_plot_fitacf,time
;
;	INPUTS:
;		time:  a string with a timestamp to be used for a file name
;
; OPTIONAL INPUTS:
;
; KEYWORD PARAMETERS:
;
; EXAMPLE:
; test_fitacf -new -hr 5 -min 3 -beam 7 myfile.rawacf > /rst/output_files/timestamp.lmfit.test
; plot_fitacf
;
; OUTPUT:
; /rst/output_plots/timestamp.fitacf.ps
;
;
; COPYRIGHT:
; Copyright (C) 2011 by Virginia Tech
;
; Permission is hereby granted, free of charge, to any person obtaining a copy
; of this software and associated documentation files (the "Software"), to deal
; in the Software without restriction, including without limitation the rights
; to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
; copies of the Software, and to permit persons to whom the Software is
; furnished to do so, subject to the following conditions:
;
; The above copyright notice and this permission notice shall be included in
; all copies or substantial portions of the Software.
;
; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
; IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
; AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
; OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
; THE SOFTWARE.
;
;
; MODIFICATION HISTORY:
; Written by AJ Ribeiro 08/29/2011
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


pro acf_plot_fitacf,time
  ;the file we are reading data from
  file_in = '/rst/output_files/'+time+'.fitacf.test'
  set_plot,'PS',/copy
  device,/landscape,/COLOR,BITS_PER_PIXEL=8,filename='/rst/output_plots/'+time+'.ps'
  S = findgen(17)*(!PI*2./16.)
  !p.multi = [0,1,1]

	;open the file
  openr,unit,file_in,/get_lun

	;read the first line
	readf,unit,stid,yr,mo,dy,hr,mt,sc,bmnum
  readf,unit,nrang,mplgs,skynoise,tfreq,mpinc,lagfr,smsep,nave,cpid,vdir
	lambda = 3.e8/(tfreq*1.e3)
	;get rad info
  radar_info,stid,glat,glon,mlat,mlon,oneletter,threeletter,name,fix(stid)

  
  if(mo lt 10) then mostr = '0'+strtrim(round(mo),2) $
  else mostr = strtrim(round(mo),2)

  if(dy lt 10) then dystr = '0'+strtrim(round(dy),2) $
  else dystr = strtrim(round(dy),2)
  
  if(hr lt 10) then hrstr = '0'+strtrim(round(hr),2) $
  else hrstr = strtrim(round(hr),2)

  if(mt lt 10) then mtstr = '0'+strtrim(round(mt),2) $
  else mtstr = strtrim(round(mt),2)

  if(sc lt 10) then scstr = '0'+strtrim(round(sc),2) $
  else scstr = strtrim(round(sc),2)

  date_str = name+'	 '+$
							strtrim(fix(yr),2)+'/'+mostr+'/'+dystr
  date_str = date_str+'	 '
  date_str = date_str+hrstr+':'+mtstr+':'+scstr+' UT'
  date_str = date_str+'		'
  date_str = date_str+'Beam: '+strtrim(fix(bmnum),2)
    date_str = date_str+'		'
  date_str = date_str+'Freq: '+strtrim(fix(tfreq),2)+' kHz'
  date_str = date_str + '  FITACF  '

  mystr = 'Nave: '+strtrim(round(nave),2)
  mystr = mystr + '  CPID: '+strtrim(round(cpid),2)+' ('+get_cp_name(round(cpid))+')'
  mystr = mystr + '  Noise: '+strtrim(round(skynoise),2)
  mystr = mystr + '  Lagfr: '+strtrim(round(lagfr),2)+' us'
  mystr = mystr + '  Smsep: '+strtrim(round(smsep),2)+' us'

	;declare the arrays
	lagnums = intarr(nrang,mplgs)
	acfs = dblarr(nrang,mplgs,2)
	fitted_acfs = dblarr(mplgs,2)
	fit_flgs = intarr(nrang)
	omega_loc = dblarr(nrang)
	sct_flgs = intarr(nrang)
	fit_params = dblarr(nrang,4)
	first_stat = intarr(nrang)
	second_stat = intarr(nrang)
	n_lags = intarr(nrang)
	bad_lags = intarr(nrang,mplgs)
	;read the rest of the file
	for i=0,nrang-1 do begin
		readf,unit,r,thresh
		readf,unit,flg
		first_stat(i) = flg
		if(flg ne 0) then continue
		;read the acfs
		for j=0,mplgs-1 do begin
			readf,unit,lag,re,im,bad
			lagnums(i,j) = lag
			acfs(i,j,0) = re
			acfs(i,j,1) = im
			bad_lags(i,j) = bad
		endfor
		readf,unit,flg
		second_stat(i) = flg
		if(flg ne 0) then continue
		for j=0,mplgs-1 do begin
			readf,unit,lag,re,im,bad
			lagnums(i,j) = lag
			acfs(i,j,0) = re
			acfs(i,j,1) = im
			bad_lags(i,j) = bad
			if(bad eq 0) then n_lags(i) = n_lags(i) + 1
		endfor
		;read the params that determine if fitting is performed
		readf,unit,flg
		fit_flgs(i) = flg
		;if a fit was performed
		if(flg eq 0) then begin
			readf,unit,o_l
			omega_loc(i) = o_l
			readf,unit,qflg
			sct_flgs(i) = qflg
			if(qflg eq 1) then begin
				;read final params
				readf,unit,v,v_e,p_l,w_l
				fit_params(i,0) = v
				fit_params(i,1) = v_e
				fit_params(i,2) = p_l
				fit_params(i,3) = w_l
			endif
		endif
	endfor
	;close the input file
  close,unit
  free_lun,unit

	;plot frontpage
  acf_plot_frontpage,yr,mo,dy,hr,mt,sc,name,bmnum,tfreq,nave,cpid,nrang,$
								skynoise,0,sct_flgs,fit_params(*,2),fit_params(*,0),$
								fit_params(*,3),n_lags,glat,lagfr,smsep


  for i=0,nrang-1 do begin
		loadct,0

		;annotate the page
		xyouts,.5,.97,date_str+'Range: '+strtrim(i,2),align=.5,charsize=.8,charthick=3.,/normal
		xyouts,.5,.93,mystr,align=.5,charsize=.8,charthick=3.,/normal

		;check fitting status
		if(first_stat(i) ne 0) then begin
			xyouts,.5,.71,'Fitting exited with status: -1 ',/normal,align=.5,charsize=1.,charthick=3.
			erase
			continue
		endif

		;plot the rawacf
		acf_plot_rawacf,acfs(i,*,0),acfs(i,*,1),mplgs,lagnums(i,*),bad_lags(i,*),[.1,.67,.83,.87]

		;check the fitting status
		if(second_stat(i) ne 0) then begin
			xyouts,.5,.71,'Fitting exited with status: 1 ',/normal,align=.5,charsize=1.5,charthick=3.
			erase
			continue
		endif

		;check fitting status
		if(fit_flgs(i) ne 0) then begin
			xyouts,.5,.30,'Fitting exited with status: '+strtrim(fix(fit_flgs(i)),2),/normal,align=.5,charsize=1.5,charthick=3.
			if(fit_flgs(i) eq 4) then $
				xyouts,.5,.25,'(not enough good lags)',/normal,align=.5,charsize=1.5,charthick=3.
			if(fit_flgs(i) eq 2) then $
				xyouts,.5,.25,'(calc_phi_res returned bad status)',/normal,align=.5,charsize=1.5,charthick=3.
			if(fit_flgs(i) eq 3) then $
				xyouts,.5,.25,'(c_log < 0)',/normal,align=.5,charsize=1.5,charthick=3.
			erase
			continue
		endif

		;calculate fitted ACF
		mag = 10.^(fit_params(i,2)/10)*skynoise
		dopfreq = 2.*!pi*(fit_params(i,0)/vdir*2./lambda)
		t_d = lambda/(2.*!pi*fit_params(i,3))
		print,mag,i,lambda,skynoise,fit_params(i,2)
		for j=0,mplgs-1 do begin
			tau = mpinc*lagnums(i,j)
			fitted_acfs(j,0) = mag*exp(-1.0*tau/t_d)*cos(tau*dopfreq)
			fitted_acfs(j,1) = mag*exp(-1.0*tau/t_d)*sin(tau*dopfreq)
		endfor

		;plot the phase panel
		acf_plot_phase_panel,atan(acfs(i,*,1),acfs(i,*,0)),atan(fitted_acfs(*,1),fitted_acfs(*,0)),$
													mplgs,lagnums(i,*),bad_lags(i,*),fit_params(i,0),omega_loc(i),fit_params(i,1),$
													[.1,.38,.39,.58]

		;plot the power panel
		acf_plot_power_panel,sqrt(acfs(i,*,1)^2+acfs(i,*,0)^2),sqrt(fitted_acfs(*,1)^2+fitted_acfs(*,0)^2),$
													mplgs,lagnums(i,*),bad_lags(i,*),fit_params(i,2),fit_params(i,3),$
													[.54,.38,.83,.58]

		;start a new page
		erase
  endfor


  ;close the postscript file
  device,/close


end